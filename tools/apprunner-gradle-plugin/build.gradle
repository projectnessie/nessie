/*
 * Copyright (C) 2020 Dremio
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java-gradle-plugin'
  id 'jacoco'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.12.0'
  id "com.diffplug.spotless" version "5.12.5"
}

def mavenPropertiesFile = file('../../target/project.properties')
if (!mavenPropertiesFile.exists() || mavenPropertiesFile.lastModified() < file("../../pom.xml").lastModified()) {
  exec {
    commandLine = ['../../mvnw', 'properties:write-project-properties', '-Pall-properties', '-q', '-f', '../../pom.xml']
    environment('JAVA_HOME', System.properties['java.home'])
  }.assertNormalExitValue()
}
def mavenProperties = new Properties()
mavenProperties.load(new StringReader(mavenPropertiesFile.text))

def assertjVersion = mavenProperties.get('assertj.version')
def jacksonVersion = mavenProperties.get('jackson.version')
def jacocoVersion = mavenProperties.get('jacoco.version')
def junitVersion = mavenProperties.get('junit.version')
version = mavenProperties.get('nessie.version')

allprojects {
  group = "org.projectnessie"
  version = version
  repositories {
    mavenCentral()
    mavenLocal()
  }
}

spotless {
  java {
    googleJavaFormat()
    licenseHeaderFile(file("../../codestyle/copyright-header-java.txt"))
  }
}

gradlePlugin {
  plugins {
    simplePlugin {
      id = 'org.projectnessie'
      implementationClass = 'org.projectnessie.quarkus.gradle.QuarkusAppPlugin'
      displayName = 'Quarkus App Runner'
      description = 'Start and stop a quarkus app as gradle tasks for integration testing'
    }
  }
}

repositories {
  mavenCentral()
  if (version.endsWith("-SNAPSHOT")) {
    mavenLocal()
  }
}

compileJava {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

test {
  systemProperties([
          'nessie-version': version,
          'junit-version': junitVersion,
          'jackson-version': jacksonVersion
  ])
  useJUnitPlatform()
}

dependencies {
  implementation "org.projectnessie:nessie-apprunner-common:${project.version}"

  testImplementation gradleTestKit()
  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testImplementation("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
  testImplementation("org.assertj:assertj-core:${assertjVersion}")
}

jacoco {
  toolVersion = jacocoVersion
}

task codeCoverageReport(type:JacocoReport) {
  executionData test
  sourceSets sourceSets.main

  reports {
    html.required = true
    xml.required = true
    csv.required = true
  }
}

check {
  dependsOn codeCoverageReport
}

pluginBundle {
  website = 'https://projectnessie.org'
  vcsUrl = 'https://github.com/projectnessie/nessie'
  tags = ['test', 'integration', 'quarkus']
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'org.projectnessie'
      artifactId = 'nessie-apprunner-gradle-plugin'
      version = version

      from components.java
    }
  }
}
