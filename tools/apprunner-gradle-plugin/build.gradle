plugins {
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.12.0'
}

def quarkusVersionBuffer = new ByteArrayOutputStream()
exec {
  commandLine = ['../../mvnw', 'help:evaluate', '-Dexpression=quarkus.version', '-q', '-DforceStdout', '-f', '../../pom.xml']
  standardOutput = quarkusVersionBuffer
}.assertNormalExitValue()

if (!file('version.txt').exists()) {
  def nessieVersionBuffer = new ByteArrayOutputStream()
  exec {
    commandLine = ['../../mvnw', 'help:evaluate', '-Dexpression=project.version', '-q', '-DforceStdout']
    standardOutput = nessieVersionBuffer
  }.assertNormalExitValue()
  version = nessieVersionBuffer.toString()
} else {
  version = file('version.txt').text.trim()
}

allprojects {
  group = "org.projectnessie"
  version = version
  repositories {
    mavenCentral()
    mavenLocal()
  }
}

gradlePlugin {
  plugins {
    simplePlugin {
      id = 'org.projectnessie'
      implementationClass = 'org.projectnessie.quarkus.gradle.QuarkusAppPlugin'
      displayName = 'Quarkus App Runner'
      description = 'Start and stop a quarkus app as gradle tasks for integration testing'
    }
  }
}

repositories {
  mavenCentral()
}

compileJava {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

dependencies {
  implementation platform("io.quarkus:quarkus-bom:${quarkusVersionBuffer}")

  compile "io.quarkus:quarkus-bootstrap-core"
  compile "org.projectnessie:nessie-apprunner-maven-plugin:$version"
  compile "org.eclipse.microprofile.config:microprofile-config-api"
}

pluginBundle {
  website = 'https://projectnessie.org'
  vcsUrl = 'https://github.com/projectnessie/nessie'
  tags = ['test', 'integration', 'quarkus']
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'org.projectnessie'
      artifactId = 'nessie-apprunner-gradle-plugin'
      version = version

      from components.java
    }
  }
}
